# Maintainer: Omeed Safi <omeed@safi.ms>
pkgbase='osafi'
pkgname=(osafi-base osafi-desktop)
pkgver=1
pkgrel=4
pkgdesc="System configuration for osafi systems"
arch=('any')
url="https://github.com/osafi/arch-pkgs"
license=('MIT')
groups=(osafi)

source=(
    osafi-desktop-bspwm-config
    osafi-desktop-sxhkd-config
    osafi-desktop-wallpaper.png
    osafi-desktop.sh
)

sha256sums=(SKIP SKIP SKIP SKIP)

package_osafi-base() {
    provides=(vim vi)
    conflicts=(vim vi)
    install=osafi-base.install
    
    # Base group
    depends=(bash bzip2 coreutils cryptsetup device-mapper dhcpcd diffutils
    e2fsprogs file filesystem findutils gawk gcc-libs gettext glibc grep gzip
    inetutils iproute2 iputils less licenses linux linux-firmware logrotate lvm2
    man-db man-pages mdadm pacman pciutils perl procps-ng psmisc s-nail sed shadow
    sysfsutils systemd-sysvcompat tar texinfo usbutils util-linux which)
    # Explicitly removed from base group: jsfutils nano netctl reiserfsprogs vi xfsprogs

    # Extra general packages
    depends+=(sudo exa dialog unzip vim)
}

package_osafi-desktop() {
    install=osafi-desktop.install
    depends=(osafi-base)

    # Login manager
    depends+=(lightdm-webkit-theme-aether xorg-server)

    # TODO: split this stuff out more (eg. 'bswpm', 'terminal', etc...)
    depends+=(bspwm sxhkd termite feh)

    # TODO: find more appropriate location for wallpaper
    install -Dm 0644 osafi-desktop-wallpaper.png "${pkgdir}/etc/osafi/wallpaper.png"

    # TODO: do correct permissions - especially for sxhkd
    install -Dm 0755 osafi-desktop-bspwm-config "${pkgdir}/etc/osafi/bspwm/bspwmrc"
    install -Dm 0755 osafi-desktop-sxhkd-config "${pkgdir}/etc/osafi/sxhkd/sxhkdrc"
    
    install -Dm 0755 osafi-desktop.sh "${pkgdir}/etc/X11/xinit/xinitrc.d/30-osafi-desktop.sh"
}
